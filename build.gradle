plugins {
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'java-library'
}

group = 'io.github.tranngockhoa'
version = '0.0.1'
sourceCompatibility = '17'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.data:spring-data-redis'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation "io.lettuce:lettuce-core"
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.apache.commons:commons-pool2'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = true
    archiveClassifier.set("")
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.hasProperty("osshUserName") ? project.property("osshUserName") : ""
                password = project.hasProperty("osshPassword") ? project.property("osshPassword") : ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.tranngockhoa'
            version '0.0.1'
            artifactId = 'spring-boot-starter-redis'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'spring-boot-starter-redis'
                description = 'spring-boot-starter-redis'
                url = 'https://github.com/TranNgocKhoa/spring-boot-starter-redis.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tranngockhoa'
                        name = 'Tran Ngoc Khoa'
                        email = 'tranngoc.khoa.spk@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TranNgocKhoa/spring-boot-starter-redis.git'
                    developerConnection = 'scm:git:ssh://github.com/TranNgocKhoa/spring-boot-starter-redis.git'
                    url = 'https://github.com/TranNgocKhoa/spring-boot-starter-redis.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}